# Maintainer: Mika Hyttinen <mika.hyttinen@gmail.com>
pkgname=cellframe-node
pkgver=5.1.355
pkgrel=1
pkgdesc="Quantum safe, service oriented blockchain platform."
arch=('x86_64' 'aarch64')
url="https://cellframe.net"
license=('unknown')
depends=(python logrotate)
makedepends=(cmake python)
provides=("cellframe-node" "cellframe-node-cli" "cellframe-node-tool")
options=(!buildflags !makeflags)
source=("git+https://gitlab.demlabs.net/cellframe/$pkgname.git")
md5sums=('SKIP')
install=$pkgname.install

pkgver() {
	cd "$pkgname"
	grep -Po '=\K[0-9]*' version.mk | tr '\n' '.' | head -c -1
}
	
prepare() {
	cd "$pkgname"
	cat << EOF > .gitmodules
[submodule "python-cellframe"]
	path = python-cellframe
	url = https://gitlab.demlabs.net/cellframe/python-cellframe.git
	branch = master
[submodule "cellframe-sdk"]
	path = cellframe-sdk
	url = https://gitlab.demlabs.net/cellframe/cellframe-sdk.git
	branch = master
[submodule "prod_build"]
	path = prod_build
	url = https://gitlab.demlabs.net/cellframe/prod_build_cellframe-node
[submodule "dap-sdk"]
	path = dap-sdk
	url = https://gitlab.demlabs.net/dap/dap-sdk
	branch = develop
[submodule "test/libdap-test"]
	path = test/libdap-test
	url = https://gitlab.demlabs.net/cellframe/libdap-test.git
	branch = master
EOF
	git submodule sync
	git submodule update --init --recursive
}

build() {
	cd "$pkgname"
	cmake -B build \
		-DCMAKE_BUILD_TYPE='Release' \
        -Wno-dev
	cmake --build build -j$(nproc)
}

package() {
	cd "$pkgname"
	DESTDIR="$pkgdir" cmake --install build

	prefix="$pkgdir/opt/$pkgname"

	install -Dm 644 $prefix/share/logrotate/$pkgname -t $pkgdir/etc/logrotate.d || return 1
	
	install -Dm 644 $prefix/share/$pkgname.service -t $pkgdir/usr/lib/systemd/system || return 1

	install -Dm 644 $prefix/share/profile.d/$pkgname.sh -t $pkgdir/etc/profile.d || return 1
}